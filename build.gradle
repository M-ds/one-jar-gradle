import org.springframework.boot.gradle.plugin.SpringBootPlugin

plugins {
    id "java"
    id "org.springframework.boot" version "2.5.5" apply false
    id "io.spring.dependency-management" version "1.0.11.RELEASE"
    id "com.github.node-gradle.node" version "3.1.1" apply false
    id 'org.siouan.frontend-jdk11' version '5.3.0'
}

allprojects {
    apply plugin: "java"
    apply plugin: "io.spring.dependency-management"

    group "nl.pbsw"
    version "0.1-SNAPSHOT"
    sourceCompatibility = "11"

    repositories {
        mavenCentral()
    }

    dependencyManagement {
        imports {
            mavenBom SpringBootPlugin.BOM_COORDINATES
        }
    }

    test {
        useJUnitPlatform()
    }
}

// Code block is responsible for building the frontend and set it to the dist folder within the frontend folder
frontend {
    packageJsonDirectory = new File('./application/frontend/')
    nodeVersion = '16.8.0'
    assembleScript = 'run build'
    cleanScript = 'run clean'
}

task moveFiles(type: Copy) {
    from(file("./application/frontend/dist"))
    into("./application/presentation/src/main/resources/static")
}
